buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "maven"
apply plugin: "java"
apply plugin: "idea"

version = "${minecraft_version}-" + getVersion()
group = "${group}"
archivesBaseName = "${mod_name}"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "forge"
        url = "http://files.minecraftforge.net/maven"
    }
    maven {
        name = "The Loader"
        url = "https://maven.blamejared.com"
    }
    maven {
        name = "Titanium"
        url = "https://cdn.hrzn.studio/maven/"
    }
    maven {
        name = "Registrate"
        url = "http://maven.tterrag.com/"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    main {
        resources {
            include '**/**'
            // copy everything else, but blender files.
            exclude '**/*.bbmodel'
            //Add the generated main module resources
            srcDirs += 'src/generated/resources/transport'
            //But exclude the cache of the generated data from what gets built
            exclude '.cache'
        }
    }
    quark {
        setupSourceSet(sourceSets.quark, 'quark', 'transport_quark')
    }
    immersiveengineering {
        setupSourceSet(sourceSets.immersiveengineering, 'immersiveengineering', 'transport_ie')
    }
}

def setupSourceSet(SourceSet sourceSet, String name, String modid) {
    sourceSet.java {
        srcDir 'src/' + name + '/java'
    }
    sourceSet.resources {
        //Add the generated module resources
        srcDirs += 'src/generated/resources/' + modid
        //But exclude the cache of the generated data from what gets built
        exclude '.cache'
    }
    sourceSet.compileClasspath += sourceSets.main.output
}

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"

    runs {
        client {
            workingDirectory project.file("run")
            mods {
                transport {
                    source(sourceSets.main)
                }
                transport_quark {
                    source(sourceSets.quark)
                }
                transport_ie {
                    source(sourceSets.immersiveengineering)
                }
            }
        }

        server {
            workingDirectory project.file("run")
            mods {
                transport {
                    source(sourceSets.main)
                }
                transport_quark {
                    source(sourceSets.quark)
                }
                transport_ie {
                    source(sourceSets.immersiveengineering)
                }
            }
        }

        data {
            workingDirectory project.file("run")
            environment 'target', 'fmluserdevdata'
            properties 'fml.earlyprogresswindow': 'false'

            args '--all', '--output', file('src/generated/resources/'),
                    '--reports',
                    '--mod', 'transport', '--existing', sourceSets.main.resources.srcDirs[0],
                    '--mod', 'transport_ie', '--existing', sourceSets.immersiveengineering.resources.srcDirs[0]

            mods {
                transport {
                    source(sourceSets.main)
                }
                transport_ie {
                    source(sourceSets.immersiveengineering)
                }
            }
        }
    }
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

configurations {

    shade
    //Make sure all our sub source set stuff extends the proper base methods so that
    // they can see all the dependencies we have in dependencies including forge
    def compileExtenders = [quarkCompile, immersiveengineeringCompile]
    def compileOnlyExtenders = [quarkCompileOnly, immersiveengineeringCompileOnly]
    def runtimeOnlyExtenders = [quarkRuntimeOnly, immersiveengineeringRuntimeOnly]
    def implementationExtenders = [quarkImplementation, immersiveengineeringImplementation]
    def shadeExtenders = [quarkShade, immersiveengineeringShade]

    for (def compileExtender : compileExtenders) {
        compileExtender.extendsFrom(compile)
    }
    for (def compileOnlyExtender : compileOnlyExtenders) {
        compileOnlyExtender.extendsFrom(compileOnly)
    }
    for (def runtimeOnlyExtender : runtimeOnlyExtenders) {
        runtimeOnlyExtender.extendsFrom(runtimeOnly)
    }
    for (def implementationExtender: implementationExtenders) {
        implementationExtender.extendsFrom(implementation)
    }
    for (def shadeExtender : shadeExtenders) {
        shadeExtender.extendsFrom(shade)
    }
}

def version_properties = [
        "version"       : mod_version,
        "mc_version"    : minecraft_version,
        "forge_version" : min_forge_version,
        "loader_version": loader_version
]

ext {
    replaceResourcesInfo = [
            "main"                : sourceSets.main,
            "quark"               : sourceSets.quark,
            "immersiveengineering": sourceSets.immersiveengineering
    ]
}

replaceResourcesInfo.each { output, sourceSet ->
    return tasks.create("${output}ReplaceResources", Copy) {
        outputs.upToDateWhen { false }
        from(sourceSet.resources) {
            include "META-INF/mods.toml"
            expand version_properties
        }
        into "$buildDir/resources/${output}/"
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'xyz.brassgoggledcoders.transport.repack.registrate'
}


dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    if (findProject(':Titanium') != null) {
        compile project(':Titanium')
    } else {
        compile fg.deobf(project.dependencies.create("com.hrznstudio:titanium:${minecraft_version}-${titanium_version}") {
            transitive = false
        })
    }
    def registrate = "com.tterrag.registrate:Registrate:MC${minecraft_version}-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    compile fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    quarkCompile fg.deobf("vazkii.autoreglib:AutoRegLib:${auto_reg_lib_version}")
    quarkCompile fg.deobf("vazkii.quark:Quark:${quark_version}")

    compile fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:1.16.1-3.1.1-pre.53")

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.1')
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${title}",
                "Specification-Vendor"    : "${team}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${team}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

artifacts {
    archives javadocJar
    archives srcJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if (branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if (buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if (buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "10000"
    }
}

wrapper {
    version('5.1.0')
}
