buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true)
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

import org.gradle.internal.xml.XmlTransformer

plugins {
    id("org.jetbrains.gradle.plugin.idea-ext") version "1.1"
}

project('API') {
    ext {
        rootConfigure = true
        publish = true
    }
}

project('Data') {
    ext {
        rootConfigure = true
        publish = true
        dependsOnApi = true
    }
}


subprojects { Project project ->
    if (project.ext.find("rootConfigure")) {
        println(project.name)

        apply plugin: 'net.minecraftforge.gradle'
        apply plugin: 'org.parchmentmc.librarian.forgegradle'

        if (project.ext.find("publish")) {
            apply plugin: 'maven-publish'
        }

        java.toolchain.languageVersion = JavaLanguageVersion.of(17)

        minecraft {
            mappings channel: 'parchment', version: '2022.06.05-1.18.2'
        }

        dependencies {
            minecraft 'net.minecraftforge:forge:1.18.2-40.1.20'

            if (project.ext.find("dependsOnApi")) {
                compileOnly rootProject.project(":API")
            }
        }

        jar.finalizedBy('reobfJar')

        if (project.ext.find("publish")) {
            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        artifact jar
                    }
                }
                repositories {
                    maven {
                        url "file://${project.projectDir}/mcmodsrepo"
                    }
                }
            }
        }
    }
}

project('runenv') {
    ext {
        mcversion = '1.18.2'
        forgeversion = '40.1.31'
    }

    repositories {
        // Repositories for additional runtime dependencies go here
    }

    afterEvaluate {
        dependencies {

        }

        minecraft.runs.forEach(run -> {
            var gameTestNamespace = [] as Set<String>
            rootProject.subprojects.forEach(p -> {
                if (p == project) return
                p.pluginManager.withPlugin('net.minecraftforge.gradle') {
                    var r = p.minecraft.runs.findByName(run.name)
                    if (r != null && r.properties.containsKey("forge.enabledGameTestNamespaces")) {
                        gameTestNamespace.addAll(r.properties.get("forge.enabledGameTestNamespaces").split(","))
                    }
                }
            })
            if (!gameTestNamespace.isEmpty()) {
                run.properties.put("forge.enabledGameTestNamespaces", gameTestNamespace.join(","))
            }
        })
    }

    tasks.register("genBuildAllIntellijRuns") {
        dependsOn("genIntellijRuns")
        group("forgegradle runs")
        doLast {
            rootProject.files(
                    ".idea/runConfigurations/runenv_runClient.xml",
                    ".idea/runConfigurations/runenv_runServer.xml"
            ).each { file ->
                if (file.exists()) {
                    var transformer = new XmlTransformer()
                    transformer.addAction { XmlProvider provider ->
                        println("Action")
                        provider.asNode()
                                .configuration
                                .method
                                .option
                                .each {
                                    if (it.@name == "Make") {
                                        it.@name = "MakeProject"
                                    }
                                }
                    }
                    file.withReader { reader ->
                        var text = reader.getText();
                        println(text)
                        file.withWriter { writer ->
                            transformer.transform(text, writer)
                        }
                    }
                }
            }
        }
    }
}